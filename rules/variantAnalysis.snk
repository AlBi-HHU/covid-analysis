def fetchAllVCFs():
    list = []
    for run in barcodes:
        list+= expand('data/input/'+run+'/result_hac/barcode{barcode}.{method}.primertrimmed.vcf',method=methods,barcode=barcodes[run])
    return list

def fetchAllLabels():
    list = []
    for run in barcodes:
        list+= expand('data/auxiliary/pileupAnalysis/{method}/'+run+'/{barcode}.labels.json',method=methods,barcode=barcodes[run])
    return list

def fetchAllPileups():
    list = []
    for run in barcodes:
        list += expand('data/auxiliary/pileupAnalysis/{method}/'+run+'/{barcode}.pileupanalysis.txt',method=methods,barcode=barcodes[run])
    return list

def fetchAllLikelihoods():
    list = []
    for run in barcodes:
        list += expand('data/auxiliary/probabilisticGenotyping/{method}/'+run+'/{barcode}.probabilities.json',method=methods,barcode=barcodes[run])
    return list

def fetchAllVCFDiffs():
    list = []
    for run in barcodes:
        list += expand('data/auxiliary/probabilisticGenotyping/{method}/'+run+'/{barcode}.vcfdiff',method=methods,barcode=barcodes[run])
    return list

rule generateIGVSessions:
    input:
        reference = 'data/input/nCoV-2019.reference.fasta',
        vcfFileNano = 'data/input/{run}/result_hac/barcode{barcode}.nanopolish.primertrimmed.vcf',
        vcfFileMeda = 'data/input/{run}/result_hac/barcode{barcode}.medaka.primertrimmed.vcf',
        alignmentFileIndex = 'data/input/{run}/result_hac/barcode{barcode}.{method}.primertrimmed.sorted.bam.bai'
    output:
        report('data/output/IgvSessions/{method}/{run}/{barcode}.igv.xml',category='IGVSessions')
    params:
        alignmentFile = 'data/input/{run}/result_hac/barcode{barcode}.{method}.primertrimmed.sorted.bam',
        # Cluster Execution
        cpus = '1',
        mem = '256M',
        gpus = '0',
        walltime = '00:05:00'
    conda:
        '../envs/perl.yaml'
    shell:
        'perl scripts/generate_IGV.pl --covidReference {input.reference} --BAM {params.alignmentFile} --output {output} --VCF_Medaka {input.vcfFileMeda} --VCF_Nano {input.vcfFileNano}'
rule getVariantPositions:
    input:
        vcfFiles = fetchAllVCFs()
    output:
        'data/auxiliary/interestingPositions.json'   #
    params:
        # Cluster Execution
        cpus = '1',
        mem = '1G',
        gpus = '0',
        walltime = '00:05:00'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/determineVariantPositions.py'

rule pileupAnalysis:
    input:
        alignment = 'data/input/{run}/result_hac/barcode{barcode}.{method}.primertrimmed.sorted.bam',
                alignment_index = 'data/input/{run}/result_hac/barcode{barcode}.{method}.primertrimmed.sorted.bam.bai',
        reference = 'data/input/nCoV-2019.reference.fasta'
    output:
        'data/auxiliary/pileupAnalysis/{method}/{run}/{barcode}.pileupanalysis.txt'
    params:
        # Cluster Execution
        cpus = '1',
        mem = '8G',
        gpus = '0',
        walltime = '00:05:00'
    conda:
        '../envs/biopythonworkbench.yaml'
    shell:
        'perl scripts/getCompleteMycoFrequencies.pl --BAM {input.alignment} --output {output} --minMappingQuality 0 --minBaseQuality 0 --referenceGenome {input.reference}'


rule labelAlleles:
    input:
        pileupAnalysis = 'data/auxiliary/pileupAnalysis/{method}/{run}/{barcode}.pileupanalysis.txt',
        vcfFile = 'data/input/{run}/result_hac/barcode{barcode}.{method}.primertrimmed.vcf'
    output:
        'data/auxiliary/pileupAnalysis/{method}/{run}/{barcode}.labels.json'
    params:
        # Cluster Execution
        cpus = '1',
        mem = '256M',
        gpus = '0',
        walltime = '00:05:00'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/labelVariantCalls.py'

rule labelSamples:
    input:
        labels = fetchAllLabels()
    output:
        'data/auxiliary/sampleClassification.tsv'
    params:
        # Cluster Execution
        cpus = '1',
        mem = '256M',
        gpus = '0',
        walltime = '00:05:00'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/labelSamples.py'

rule calculateSplitLikelihoods:
    input:
        pileup = 'data/auxiliary/pileupAnalysis/{method}/{run}/{barcode}.pileupanalysis.txt',
        emissions = 'data/auxiliary/unifiedEmissions.json'
    output:
        likelihoods = 'data/auxiliary/probabilisticGenotyping/{method}/{run}/{barcode}.probabilities.json',
        figures = 'data/output/probabilisticGenotyping/{method}/{run}/{barcode}.probabilities.pdf'
    params:
        # Cluster Execution
        cpus = '1',
        mem = '2G',
        gpus = '0',
        walltime = '00:30:00'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/calculateSplitLikelihoods.py'

rule probabilisticGenotyping:
    input:
        reference = 'data/input/nCoV-2019.reference.fasta',
        likelihoods = 'data/auxiliary/probabilisticGenotyping/{method}/{run}/{barcode}.probabilities.json'
    output:
        vcf = 'data/output/probabilisticGenotyping/{method}/{run}/{barcode}.vcf',
        mix = 'data/output/probabilisticGenotyping/{method}/{run}/{barcode}.mix.svg'
    params:
        # Cluster Execution
        cpus = '1',
        mem = '2G',
        gpus = '0',
        walltime = '00:30:00'
    benchmark:
        'benchmarks/probabilisticGenotyping_{method}_{run}_{barcode}.txt'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/probabilisticGenotyping.py'

rule diffGenotypingVCFs:
    input:
        newVCF = 'data/output/probabilisticGenotyping/{method}/{run}/{barcode}.vcf',
        originalVCF = 'data/input/{run}/result_hac/barcode{barcode}.{method}.primertrimmed.vcf'
    output:
        'data/auxiliary/probabilisticGenotyping/{method}/{run}/{barcode}.vcfdiff'
    conda:
        '../envs/pyvcf.yaml'
    script:
        '../scripts/vcfdiff.py'

rule aggregateVCFDiffs:
    input:
        fetchAllVCFDiffs()
    output:
        'data/output/probabilisticGenotyping/aggregatedDiffs.json'
    conda:
        '../envs/pyvcf.yaml'
    script:
        '../scripts/aggregateDiffs.py'

rule annotateDiffs:
    input:
        pileups = fetchAllPileups(),
        diffs = 'data/output/probabilisticGenotyping/aggregatedDiffs.json'
    output:
        diffs = 'data/output/probabilisticGenotyping/annotatedDiffs.json'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/annotateDiffs.py'

rule outputUndeterminedVariantPositions:
    input:
        emissions = 'data/auxiliary/unifiedEmissions.json',
        variantPositions = 'data/auxiliary/interestingPositions.json'
    output:
        undecidablePositions = 'data/auxiliary/undecidablePositions.json'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/assembleUndecidablePositions.py'

rule assembleUnifiedEmissions:
    input:
        refEmissions = 'data/auxiliary/referenceEmissions.json',
        varEmissions = 'data/auxiliary/interestingHOMPositions.json'
    output:
        'data/auxiliary/unifiedEmissions.json'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/unifyEmissions.py'

rule histogram1:
    input:
        likelihoods = fetchAllLikelihoods()
    output:
        figures = 'data/output/splitLikelihoodsGlobalView.pdf'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/histogram1.py'

rule getHOMVariantPositions:
    input:
        labels = fetchAllLabels()
    output:
        'data/auxiliary/interestingHOMPositions.json'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/assembleHOMPositions.py'

rule averageCoveragePlot:
    input:
        pileups = fetchAllPileups()
    output:
        'data/output/coverageAverages.svg'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/coveragePlot.py'   

rule assembleReferenceEmissions:
    input:
        pileups = fetchAllPileups(),
        reference = 'data/input/nCoV-2019.reference.fasta',
        homPositions = 'data/auxiliary/interestingHOMPositions.json',
        variantPositions = 'data/auxiliary/interestingPositions.json'
    output:
        referenceEmissions = 'data/auxiliary/referenceEmissions.json'
    conda:
        '../envs/biopythonworkbench.yaml'
    script:
        '../scripts/assembleReferenceEmissions.py'
